/*
 *  b0rk - The b0rk Embeddable Runtime Environment
 *  Copyright (C) 2015, 2016 GeekProjects.com
 *
 *  This file is part of b0rk.
 *
 *  b0rk is free software: you can redistribute it and/or modify
 *  it under the terms of the GNU Lesser General Public License as published by
 *  the Free Software Foundation, either version 3 of the License, or
 *  (at your option) any later version.
 *
 *  b0rk is distributed in the hope that it will be useful,
 *  but WITHOUT ANY WARRANTY; without even the implied warranty of
 *  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *  GNU Lesser General Public License for more details.
 *
 *  You should have received a copy of the GNU Lesser General Public License
 *  along with b0rk.  If not, see <http://www.gnu.org/licenses/>.
 */


import system.io.File;
import system.lang.Maths;

class tests.Mandelbrot
{
    var maxIterations;

    function Mandelbrot()
    {
        maxIterations = 20;
    }

    function iterate(px, py)
    {
        var x = 0.0;
        var y = 0.0;
        var iteration = 0;
        while ((((x * x) + (y * y)) < (2.0 * 2.0)) && iteration < maxIterations)
        {
            var xtemp = ((x * x) - (y * y)) + px;
            y = (2.0 * x * y) + py;
            x = xtemp;
            iteration++;
        }
        return iteration;
    }

    function generate()
    {
        var minX = -2.0;
        var minY = -2.0;
        var maxX = 2.0;
        var maxY = 2.0;
        var step = 0.005;
        //var step = 0.01;

        var width = (maxX - minX) / step;
        var height = (maxY - minY) / step;

        File.out.writeln("P2");
        File.out.write(Maths.round(width));
        File.out.write(" ");
        File.out.writeln(Maths.round(height));
        File.out.writeln(maxIterations);
        var py;
        for (py = minY; py <= maxY - step; py += step)
        {
            var px;
            for (px = minX; px <= maxX - step; px += step)
            {
                File.out.writeln(iterate(px, py));
            }
        }
    }

    function main() static
    {
        var mandelbrot = new tests.Mandelbrot();
        mandelbrot.generate();
    }
}

